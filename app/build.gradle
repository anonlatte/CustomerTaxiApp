apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.google.gms.google-services'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId 'com.example.customers_app'
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField("String", "GoogleMapsKey", keystoreProperties.getProperty("GOOGLE_MAPS_API_KEY"))
            resValue("string", "google_maps_key", keystoreProperties.getProperty("GOOGLE_MAPS_API_KEY"))
            buildConfigField("String", "ApiVersion", keystoreProperties.getProperty("API_VERSION"))
            buildConfigField("String", "ServerAddress", keystoreProperties.getProperty("ServerAddress"))
            buildConfigField("int", "ServerPort", keystoreProperties.getProperty("ServerPort"))
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "GoogleMapsKey", keystoreProperties.getProperty("GOOGLE_MAPS_API_KEY"))
            resValue("string", "google_maps_key", keystoreProperties.getProperty("GOOGLE_MAPS_API_KEY"))
            buildConfigField("String", "ApiVersion", keystoreProperties.getProperty("API_VERSION"))
            buildConfigField("String", "ServerAddress", keystoreProperties.getProperty("ServerAddress"))
            buildConfigField("int", "ServerPort", keystoreProperties.getProperty("ServerPort"))
        }
    }

}

repositories {
    maven {
        url "http://maven.google.com/"
    }
    mavenCentral()
    maven { url "https://jitpack.io" }
}

ext {
    grpcVersion = '1.25.0'
    protocVersion = '3.11.4'
    daggerVersion = '2.27'
    playServices = '17.0.0'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protocVersion" }
    plugins {
        javalite { artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0" }
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc { // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'com.google.android.material:material:1.1.0'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Firebase
    implementation 'com.google.firebase:firebase-analytics:17.3.0'

    // GRPC and Protobuf
    implementation "io.grpc:grpc-protobuf-lite:$grpcVersion"
    implementation "io.grpc:grpc-okhttp:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    protobuf "com.google.protobuf:protobuf-java:$protocVersion"

    // Dagger 2
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    // Google maps and places API
    implementation 'com.google.maps:google-maps-services:0.11.0'
    implementation "com.google.android.gms:play-services-maps:$playServices"
    implementation "com.google.android.gms:play-services-location:$playServices"
    implementation "com.google.android.gms:play-services-places:$playServices"
    implementation 'com.google.android.libraries.places:places:2.2.0'

    // For phone country codes
    implementation 'com.github.joielechong:countrycodepicker:2.3.0'
}
